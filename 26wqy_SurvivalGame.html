<!DOCTYPE html>
<!--表示文档类型为HTML文档-->
<html>
<head>
    <!--文档头部-->
    <!--使用UTF-8字符编码，无此句在某些浏览器会出现乱码-->
    <meta charset="utf-8">
    <!--文档标题，会显示在网页标题栏-->
    <title>2018031701026吴倩莹</title>
</head>
<body>
    <!--文档主体-->
    <!--绘图区域的id，以及宽度和高度，用WebGL绘制的图形将显示在该canvas中-->
    <canvas id="webgl" width="1600" height="900"
    style="position:absolute;z-index:0">
  对不起，你的浏览器不支持HTML5的canvas元素！<!--出错时显示这句话-->
  </canvas>
  <canvas id="hud" width="1600" height="900"
    style="position:absolute;z-index:1">
  对不起，你的浏览器不支持HTML5的canvas元素！<!--出错时显示这句话-->
  </canvas>
    <!--以下为用到的shader程序和JavaScript程序，程序类型由type属性决定，
		顶点Shader程序和片元Shader程序有id，因为在JavaScript程序中需要对其
		进行访问，而JavaScript程序无id-->

    <!--顶点Shader程序-->
    <script id="vertex-shader" type="x-shader/x-vertex">

      const int LightNum = 3;//光源数量
        attribute vec3 a_Position;
        attribute vec3 a_Normal;
        attribute vec2 a_Texcoord;// 顶点纹理坐标

        varying vec3 v_fN;
        varying vec3 v_fE;
        varying vec3 v_fL[LightNum];
        varying float v_Dist;//顶点到手电筒光源距离
        varying vec2 v_Texcoord;// 输出纹理坐标

        uniform mat4 u_ModelView;
        uniform mat4 u_Projection;
        uniform mat3 u_NormalMat;
        uniform bool u_bOnlyTexture;//是否只用纹理
        varying float v_Dist2;//当前点与视点的距离

        uniform vec4 u_LightPosition[LightNum];
        void main(){
           vec3 pos=(u_ModelView * vec4(a_Position,1.0)).xyz;
           v_Texcoord = a_Texcoord;
            if(!u_bOnlyTexture){
              v_fE = normalize(-pos); 
              v_fN = normalize(u_NormalMat * a_Normal);
              
              for(int i=0;i<LightNum;i++){
                if(u_LightPosition[i].w!=0.0)//近距离光源
                  v_fL[i] = normalize(u_LightPosition[i].xyz - pos); 
                else//远距离光源
                  v_fL[i] = normalize(u_LightPosition[i].xyz);
                }
                //计算顶点到手电筒光源的距离
            v_Dist = distance(pos,u_LightPosition[2].xyz);
            }   
            //裁剪坐标系下顶点坐标
			      gl_Position = u_Projection * vec4(pos,1.0) ;
			      v_Dist2=gl_Position.w;
			      v_Texcoord=a_Texcoord;
           }
    </script>
    <!--片元Shader程序-->
    <script id="fragment-shader" type="x-shader/x-fragment">
      const int LightNum = 3;//光源数量
        precision mediump float;
        varying vec3 v_fN;
        varying vec3 v_fE;
        varying vec3 v_fL[LightNum];
        varying float v_Dist;//顶点到手电筒光源距离
        varying vec2 v_Texcoord;// 输入片元纹理坐标
        uniform float u_sunRate;	//阳光照射率
        uniform float u_Shininess;
        //光源亮度与材质反射系数的乘积
        uniform vec3 u_AmbientProduct[LightNum];
        uniform vec3 u_DiffuseProduct[LightNum];
        uniform vec3 u_SpecularProduct[LightNum];
        uniform vec3 u_Emission;
        uniform sampler2D u_Sampler;// 2D纹理采样器
        uniform bool u_LightOn[LightNum];//光源开关        
        //最后一个光源的聚光灯参数
        uniform vec3 u_SpotDirection;//聚光灯照射方向（观察坐标系
        uniform float u_SpotCutOff;//聚光灯截止角度
        uniform float u_SpotExponent;//聚光灯衰减指数

        uniform float u_Alpha;//透明度
        uniform bool u_bOnlyTexture;//是否只用纹理

        //雾绘制
        uniform bool u_bFogOn;  //绘制开关
		    uniform vec3 u_FogColor;//雾的颜色
	    	uniform vec2 u_FogDist;//雾的起点和终点
		    varying float v_Dist2;//视点到雾化终点的距离
		

        void main(){
          if(u_bOnlyTexture){
            gl_FragColor = texture2D(u_Sampler,v_Texcoord);
            return ;
          }
          vec3 N = normalize(v_fN);
          vec3 E = normalize(v_fE);
          //用于累加光照颜色，初始为0
          vec3 fragColor = vec3(0.0,0.0,0.0);
          vec3 specular = vec3(0.0,0.0,0.0);//镜面光分量单独累加
          //针对每一个光源进行光照计算
          //并将计算得到的颜色累加到fragColor上
          for(int i=0;i<LightNum;i++){
            if(!u_LightOn[i]) continue;//光源关闭，则不计算该光源的贡献
            vec3 L = normalize(v_fL[i]);
            vec3 H = normalize(L + E);//半角向量

            float KSpot = 1.0;//受聚光灯影响的衰减系数（1.0即为不衰减
            if(i==2){
              //对照射方向归一化并反向（因为L也是从顶点指向光源
              vec3 spotDir = -normalize(u_SpotDirection);
              float cutoff = radians(u_SpotCutOff);//角度转弧度
              float c = dot(L,spotDir);//偏离角的cos值
              if(c<cos(cutoff))
              KSpot=0.0;//完全衰减
              else{
              float d= 1.0+0.5*v_Dist;
              KSpot = max(pow(c,u_SpotExponent),0.0)/d;
              }
            }

            //环境反射分量
            vec3 ambient = u_AmbientProduct[i];
            //漫反射分量
            float Kd = max(dot(L,N),0.0);
            vec3 diffuse = KSpot * Kd * u_DiffuseProduct[i];

            //镜面反射分量            
            if(Kd != 0.0){
              float Ks = pow(max(dot(N,H),0.0),u_Shininess);
              specular += KSpot * Ks * u_SpecularProduct[i];
            }
            //得到最终颜色
            fragColor += ambient + diffuse ;
          }
        fragColor += u_Emission;//加上物体自身的发射光;

        gl_FragColor = vec4(fragColor,1.0) * texture2D(u_Sampler,v_Texcoord)+vec4(specular,1.0); 
          gl_FragColor.a = u_Alpha;

          if(u_bFogOn){//雾气开启
            float fogFactor=clamp((u_FogDist.y-v_Dist2)/(u_FogDist.y-u_FogDist.x),0.0,1.0);
            vec3 color=mix(u_FogColor,vec3(gl_FragColor),fogFactor);
            gl_FragColor=vec4(color,gl_FragColor.a);
        }
      }

    </script>

    <script id="fragment-shaderNew" type="x-shader/x-fragment">
      const int LightNum = 3;//光源数量
        precision mediump float;
        varying vec3 v_fN;
        varying vec3 v_fE;
        varying vec3 v_fL[LightNum];
        varying float v_Dist;//顶点到手电筒光源距离
        varying vec2 v_Texcoord;// 输入片元纹理坐标

        //雾绘制
        uniform bool u_bFogOn;  //绘制开关
		    uniform vec3 u_FogColor;//雾的颜色
	    	uniform vec2 u_FogDist;//雾的起点和终点
		    varying float v_Dist2;//视点到雾化终点的距离
      
        //3个光源的环境光、漫反射光和镜面反射光
        uniform vec3 u_AmbientLight[LightNum];
        uniform vec3 u_DiffuseLight[LightNum];
        uniform vec3 u_SpecularLight[LightNum];
        //物体材质
        uniform vec3 u_Ka;    //环境光反射系数
        uniform vec3 u_Kd;    //漫反射系数
        uniform vec3 u_Ks;    //镜面反射系数
        uniform vec3 u_Ke;    //发射系数
        uniform float u_Ns;    //高光系数
        uniform float u_d;    //透明度
        uniform float u_sunRate;	//阳光照射率

        uniform sampler2D u_Sampler;// 2D纹理采样器
        uniform bool u_LightOn[LightNum];//光源开关        
        //最后一个光源的聚光灯参数
        uniform vec3 u_SpotDirection;//聚光灯照射方向（观察坐标系
        uniform float u_SpotCutOff;//聚光灯截止角度
        uniform float u_SpotExponent;//聚光灯衰减指数

        void main(){        
          vec3 N = normalize(v_fN);
          vec3 E = normalize(v_fE);
          //用于累加光照颜色，初始为0
          vec3 fragColor = vec3(0.0,0.0,0.0);
          vec3 specular = vec3(0.0,0.0,0.0);//镜面光分量单独累加
          //针对每一个光源进行光照计算
          //并将计算得到的颜色累加到fragColor上
          for(int i=0;i<LightNum;i++){
            if(!u_LightOn[i]) continue;//光源关闭，则不计算该光源的贡献
            vec3 L = normalize(v_fL[i]);
            vec3 H = normalize(L + E);//半角向量

            float KSpot = 1.0;//受聚光灯影响的衰减系数（1.0即为不衰减
            if(i==2){
              //对照射方向归一化并反向（因为L也是从顶点指向光源
              vec3 spotDir = -normalize(u_SpotDirection);
              float cutoff = radians(u_SpotCutOff);//角度转弧度
              float c = dot(L,spotDir);//偏离角的cos值
              if(c<cos(cutoff))
              KSpot=0.0;//完全衰减
              else{
              float d= 1.0+0.5*v_Dist;
              KSpot = max(pow(c,u_SpotExponent),0.0)/d;
              }
            }

            //环境反射分量
            vec3 ambient = u_AmbientLight[i] * u_Ka;
            //漫反射分量
            float Fd = max(dot(L,N),0.0);
            vec3 diffuse = KSpot * Fd * u_DiffuseLight[i] * u_Kd;

            //镜面反射分量            
            if(Fd != 0.0){
              float Fs = pow(max(dot(N,H),0.0),u_Ns);
              specular += KSpot * Fs * u_SpecularLight[i] * u_Ks;
            }
            //得到最终颜色
            fragColor += ambient + diffuse ;
          }
        fragColor += u_Ke;//加上物体自身的发射光;

        gl_FragColor = vec4(fragColor,1.0) * texture2D(u_Sampler,v_Texcoord)+vec4(specular,1.0); 
          gl_FragColor.a = u_d;
          if(u_bFogOn){
            float fogFactor=clamp((u_FogDist.y-v_Dist2)/(u_FogDist.y-u_FogDist.x),0.0,1.0);
            vec3 color=mix(u_FogColor,vec3(gl_FragColor),fogFactor);
            gl_FragColor=vec4(color,gl_FragColor.a);
          }
      }

    </script>
    <!--下面是辅助我们进行程序编写的4个JavaScript程序-->
    <script type="text/javascript" src="./Common/webgl-utils.js"></script>
    <script type="text/javascript" src="./Common/initShaders.js"></script>
    <script type="text/javascript" src="./Common/MV.js"></script>
    <script type="text/javascript" src="./Common/ObjModel.js"></script>
    <!--主程序-->
    <script type="text/javascript" src="26wqy_SurvivalGame.js"></script>
</body>

</html>